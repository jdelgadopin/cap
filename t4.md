class: center, up

# CAP - T√®cniques de Programaci√≥ amb FOS

![:scale 40%](figures/clojure_logo.png)

**Jordi Delgado**, **Gerard Escudero**,

.large[<ins>Tema 4</ins>]

![:scale 40%](figures/fib50anysUPC.png)

---

## _Closures_: Divideix i ven√ß


Esperem que estigui clar a hores d'ara que la capacitat de fer
servir funcions d'ordre superior ens permet treballar a nivells
d'abstracci√≥ m√©s "_alts_" dels que podem assolir si no tenim aquesta
capacitat.

Un dels exemples m√©s clars d'aix√≤ √©s la possibilitat d'"_algorismitzar_" esquemes de
disseny algor√≠smic. Veiem com passar de l'esquema al programa amb l'esquema de **_Dividir i V√®ncer_**.red[*].

_In computer science, **divide and conquer** is an **algorithm design
paradigm**. A divide-and-conquer algorithm recursively breaks down a
problem into two or more sub-problems of the same or related type,
until these become simple enough to be solved directly. The solutions
to the sub-problems are then combined to give a solution to the
original problem_.

Exemples coneguts de l'aplicaci√≥ d'aquest esquema s√≥n: la cerca bin√†ria, Quicksort,
Mergesort, producte r√†pid de Karatsuba, la multiplicaci√≥ de matrius de Strassen, l'algorisme de
Cooley-Tukey per calcular la transformada r√†pida de Fourier, etc.

Anem a veure com transformar aquest esquema, o _design paradigm_, en
un programa. Farem servir el cas m√©s senzill, deixem com a
**exercici** la seva generalitzaci√≥.

.footnote[.red[*] [Font](https://en.wikipedia.org/wiki/Divide-and-conquer_algorithm)]

---

## _Closures_: Divideix i ven√ß

Suposem que els valors sobre els que treballarem s√≥n **vectors**. I suposem tamb√© que disposem
de les seg√ºents quatre funcions: 

* `trivial`: Funci√≥ que detecta el cas trivial, o cas base, del problema
* `directe`: Acci√≥ a realitzar en el cas base.
* `dividir`: Si no estem en el cas base, cal dividir el problema en dos sub-problemes.
* `v√®ncer`: Acci√≥ a realitzar per composar les solucions dels subproblemes.

Aleshores, donat un problema concret sobre vectors pel que hem
definit les funcions anteriors, i l'esquema en forma de programa `dIv`
(encara per definir), el programa per resoldre el problema seria:

```Clojure
(def soluci√≥ (dIv trivial directe dividir v√®ncer))
```
Concretem-ho amb un exemple cl√†ssic: **Quicksort**

---

## _Closures_: Divideix i ven√ß

Les funcions anteriors pel cas particular del _Quicksort_ podrien ser:

```Clojure
(def trivial #(< (count %) 2)) ;; vector buit o amb un sol element

(def directe identity)         ;; no cal fer res en el cas base

(defn dividir
  "vct t√© m√©s d'un element"
  [vct]
  (let [[x & rstv] vct         ;; el pivot ser√† el primer element del vector
        menors (filter #(<= % x) rstv)
        majors (filter #(> % x)  rstv)]
    [menors,majors]))          ;; retornem les dos subvectors

(defn v√®ncer
  "Pel quicksort no ens importa la parella de vectors originals"
  [vct _ [sol1 sol2]]
  (concat sol1 [(first vct)] sol2))  ;; combinem solucions amb el pivot
```
Aleshores...
```Clojure
(def quicksort (dIv trivial directe dividir v√®ncer))

(let [s  (shuffle (range 20))]
   (println s)
   (quicksort s))
üëÅÔ∏è [9 6 15 3 2 4 13 5 17 7 11 1 14 12 0 16 18 10 19 8]
üëâ (0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19)
```

---

## _Closures_: Divideix i ven√ß

Finalment, com √©s `dIv`? Naturalment √©s una _closure_ on, donades les
funcions mencionades, es retorna una funci√≥ que nom√©s requereix
l'entrada del problema (en aquest cas un vector, o seq√º√®ncia). Com hem
dit abans, aquest √©s el cas m√©s senzill, on suposem que la
descomposici√≥ del problema √©s en dos subproblemes:

```Clojure
(defn dIv
  [trivial directe dividir v√®ncer]
  (letfn [(dIv' [vct]
            (if (trivial vct)
              (directe vct)
              (let [[x1 x2] (dividir vct)
                    y1 (dIv' x1)
                    y2 (dIv' x2)]
                (v√®ncer vct [x1,x2] [y1,y2]))))]
    dIv'))
```
Fixem-nos que √©s pr√†cticament una _transcripci√≥ literal_ de l'esquema de _dividir i v√®ncer_ 
que tots coneixem.

Aquest √©s un gran exemple de fins a quin punt tenir funcions d'ordre superior pot canviar la nostra
manera de programar.

---

## _Closures_: _Backtracking_

Explorem un exemple m√©s de transformacions d'esquema algor√≠smic en
programa: l'esquema de **_Backtracking_** (_tornada enrera_).red[*].

Aquests algorismes, en general, s√≥n algorismes que enumeren solucions
parcials a un determinat problema, decidint si val la pena continuar
completant les solucions o no. En cas negatiu, es torna enrera a mirar
de construir noves solucions re-avaluant decisions preses
anteriorment.

√âs molt similar a una cerca en profunditat sobre un graf _impl√≠cit_.
Aquest graf impl√≠cit ve donat per una funci√≥ `successor`, que, donada
una soluci√≥ parcial, permet generar els _ve√Øns_ del node actual, √©s a
dir, solucions m√©s completes donada la validesa de la soluci√≥ parcial
que representa el node actual. El resultat √©s una exploraci√≥ parcial
d'un _arbre impl√≠cit de cerca_, on no cal generar totes les branques
possibles (s'esporguen les que depenen de solucions parcials no
v√†lides).

Apliquem-ho a un problema cl√†ssic: **_Les $n$-reines_**. 

Recordeu: En un tauler $n \times n$ cal posar $n$ reines de manera que
no es matin entre elles.

.footnote[.red[*] [Font](https://en.wikipedia.org/wiki/Backtracking)]

---

## _Closures_: _Backtracking_

Suposem que disposem d'un programa anomenat `bTck` amb par√†metres la funci√≥ `successor` i
una funci√≥ que sigui capa√ß de decidir si hem trobat l'objectiu `objectiu`.
Qualsevol altre informaci√≥ dependr√† del problema. En particular, cal decidir com es representen
els nodes del graf impl√≠cit. Aquesta funci√≥ retorna una _closure_ que t√© l'estat inicial
com a par√†metre.

En el cas de les $n$-reines representarem la informaci√≥ amb un vector `[col n [[c1 f1] [c2 f2]...[ck fk]]]`
on `col` representa la columna actual on volem posar la reina $k+1$-√®ssima, considerant que $k$ reines ja
estan ben posades, la qual cosa representem amb la soluci√≥ parcial `[[c1 f1] [c2 f2]...[ck fk]]`. Les funcions
`successor`, `objectiu` i l'estat inicial dependran d'aquesta representaci√≥. Cada soluci√≥ completa ser√† un 
vector `[n+1 n [[c1 f1] [c2 f2]...[cn fn]]]`.

Les funcions que defineixen l'estat inicial i el fet d'arribar a l'objectiu s√≥n molt senzilles:

```Clojure
(defn initialNQ [n]  ;; columna 1, soluci√≥ parcial buida []
  [1 n []])

(defn objNQ [[c n psol]]  ;; acabem quan la columna a considerar c > n
  (> c n))                ;; la soluci√≥ parcial psol associada √©s completa
```

---

## _Closures_: _Backtracking_

La funci√≥ `successor` senzillament mira de posar la reina a les diferents files de la columna `c` de 
manera que la proposta `[c f]` sigui compatible/v√†lida respecte a la soluci√≥ parcial `psol`. Per a aix√≤
es defineix la funci√≥ `valid` que controla qu√® es genera a la llista per comprensi√≥.

```Clojure
(defn succNQ [[c n psol]]  ;; donats una columna, n i una soluci√≥ parcial
  (letfn [(valid [psol [c r]]
            (letfn [(test [[c' r']]
                      (and (not= (+ c' r') (+ c r))
                           (not= (- c' r') (- c r))
                           (not= r' r)))]
              (every? identity (map test psol))))]
    (for [r (range 1 (inc n)) :when (valid psol [c r])]
      [(inc c) n (conj psol [c r])])))
```

Aix√≠ doncs ja podem definir les funcions que resolen el problema de les $n$-reines, donat $n$:

```Clojure
(defn solucio-NReines [n]
  (let [[[_ _ s] & cua] ((bTck succNQ objNQ) (initialNQ n))]
    s))

(defn nombre-solucions-NReines [n]
  (count ((bTck succNQ objNQ) (initialNQ n))))
```
---

## _Closures_: _Backtracking_

La funci√≥ `bTck` que representa l'esquema de _Backtracking_ √©s molt similar a una cerca en profunditat,
per√≤ aquest cop el graf no est√† expl√≠cit, i nom√©s disposem de la funci√≥ `succ`. Si ens fixem,
fem servir, un cop m√©s, un vector com a pila (fent servir `conj`, `pop` i `peek`):

```Clojure
(defn bTck [succ obj]   ;; ja coneixeu flip i foldr, les fem servir aqu√≠
  (letfn [(bTck' [v]
            (cond
              (empty? v)     []
              (obj (peek v)) (conj (bTck' (pop v)) (peek v))
              :else          (let [x (peek v)]
                               (recur (foldr (flip conj) (pop v) (succ x))))))]
    (fn [inicial]
      (bTck' [inicial]))))
```

Ara ja podem fer servir les funcions que resolen el problema:

```Clojure
(solucio-NReines 8) üëâ [[1 8] [2 4] [3 1] [4 3] [5 6] [6 2] [7 7] [8 5]]

(nombre-solucions-NReines 8)  üëâ 92
(nombre-solucions-NReines 11) üëâ 2680
(nombre-solucions-NReines 12) üëâ 14200 ;; a la frontera de l'StackOverflow (!)

```

---

## _Continuation-Passing Style_ (CPS)

A la [Wikipedia](https://en.wikipedia.org/wiki/Continuation-passing_style) diu:

_In functional programming, **continuation-passing style** (CPS) is a
style of programming **in which control is passed explicitly in the form
of a continuation**. This is contrasted with direct style, which is the
usual style of programming. Gerald Jay Sussman and Guy L. Steele, Jr.
coined the phrase in AI Memo 349 (1975)_

Una funci√≥ escrita en CPS requereix un par√†metre addicional: Una
**_continuaci√≥_ expl√≠cita**, que √©s una funci√≥ d'un par√†metre.red[*].

Les funcions en CPS <ins>_no retornen mai_</ins>. Un cop han acabat de calcular el que sigui
que calculin, <ins>_cal invocar la continuaci√≥ amb aquest resultat_</ins>. Per exemple:

```Clojure
;; La funci√≥ identitat, en CPS
(defn identity-cps [x,cont]
  (cont x))
  
(identity-cps "Hola M√≥n!", identity) üëâ "Hola M√≥n!"
(identity-cps "Hola M√≥n!", #(apply str (concat % " Josep"))) üëâ "Hola M√≥n! Josep"
```
.footnote[.red[*Font]: _The Joy of Clojure_, secci√≥ 7.3.4, p. 163]

---

## _Continuation-Passing Style_: Exemples

Veiem alguns exemples una miqueta m√©s interessants que la `identity-cps`:

El factorial:
```Clojure
(defn fact-cps [n cont]
  (if (< n 2)
    (cont 1)
    (recur (dec n) (fn [m]
                     (cont (* n m))))))

(fact-cps  5 identity) üëâ 120
(fact-cps 10 identity) üëâ 3628800
(fact-cps 10 #(/ % 2)) üëâ 1814400
```
El coeficient binomial $\binom{n}{k} = \frac{n!}{k!(n-k)!}$
```Clojure
(defn binomial-coef-cps [n k cont]
  (fact-cps n (fn [factn]
                (fact-cps k 
                    (fn [factk]
                      (fact-cps (- n k) 
                          (fn [factnk] (cont (/ factn (* factk factnk))))))))))
                    
(binomial-coef-cps 7 4 identity) üëâ 35
(binomial-coef-cps 6 3 identity) üëâ 20
```
---

## _Continuation-Passing Style_: Exemples

Sigui $n$ qualsevol natural estrictament positiu. Considereu el proc√©s
seg√ºent: Si $n$ √©s parell, dividiu-lo per dos. Altrament,
multipliqueu-lo per 3 i sumeu-li 1. Quan arribeu a 1, pareu.

Per exemple, comen√ßant en $n=3$, s‚Äôobt√© la seq√º√®ncia de Collatz $S(3):
3,10,5,16,8,4,2,1$. La mida d‚Äôaquesta seq√º√®ncia √©s 7. Des de l‚Äôany 1937 es conjectura que aquest proc√©s acaba per a
qualsevol $n$ inicial, encara que no ho ha sabut demostrar ning√∫. En
aquest problema suposarem que la conjectura √©s certa.

Escriu una funci√≥ `mida-collatz` que, donat un natural $n > 0$, retorni
la mida de la seq√º√®ncia de Collatz corresponent a $n$, √©s a dir, quantes
iteracions del proc√©s descrit m√©s amunt calen per arribar a 1.

.cols5050[
.col1[
```Clojure
(defn mida-collatz [n]
  (if (== n 1) 0
    (let [nxt (if (zero? (mod n 2)) 
                (quot n 2) 
                (inc (* 3 n)))]
      (inc (mida-collatz nxt)))))

(mida-collatz 97)  üëâ 118
(mida-collatz 871) üëâ 178
```
]
.col2[
```Clojure
(defn mida-collatz-cps [n cont]
  (if (== n 1) (cont 0)
    (let [nxt (if (zero? (mod n 2)) 
                (quot n 2) 
                (inc (* 3 n)))]
      (recur nxt (fn [v] 
                   (cont (inc v)))))))

(mida-collatz-cps 97 identity)  üëâ 118
(mida-collatz-cps 871 identity) üëâ 178
```
]]

---

## _Continuation-Passing Style_: Exemples

El CPS pot ser √∫til en cas de voler interrompre l'execuci√≥ d'una funci√≥. 

Veiem un exemple:
Suposem seq√º√®ncies "_multi-nivell_" amb nombres. Per exemple: 
`'(((1)) 2 ((3 4) (5 6) ((((7)))) (((8)) 9) 10))`. Volem fer una funci√≥ que multipliqui
tots els nombres d'aquestes seq√º√®ncies.

Aquesta pot ser una soluci√≥:

```Clojure
(defn producte-seq
  "s √©s una seq√º√®ncia 'multi-nivell' de nombres, o un nombre"
  [s]
  (cond
    (number? s) s
    :else (if (empty? s) 1
              (let [[cap & cua] s
                    prod-cap    (producte-seq cap)
                    prod-cua    (producte-seq cua)]
                (* prod-cap prod-cua)))))
                
(producte-seq '(((1)) 2 ((3 4) (5 6) ((((7)))) (((8)) 9) 10))) üëâ 3628800
(producte-seq '())   üëâ 1
(producte-seq  3 )   üëâ 3
(producte-seq '(10)) üëâ 10
```
Si un dels nombres √©s `0` podr√≠em retornar immediatament, sense procedir amb el
que queda de c√†lcul. Aquesta soluci√≥, per√≤, no ho fa.

---

## _Continuation-Passing Style_: Exemples

Podem passar aquesta funci√≥ a CPS:

```Clojure
(defn producte-seq-cps
  "s √©s una seq√º√®ncia multi-nivell de nombres, o un nombre"
  [s cont]
  (cond
    (number? s) (cont s)
    :else (if (empty? s) (cont 1)
              (let [[cap & cua] s]
                (recur cap (fn [v]
                             (producte-seq-cps cua (fn [w]
                                                     (cont (* v w))))))))))
                                                     
(producte-seq-cps '(((1)) 2 ((3 4) (5 6) ((((7)))) (((8)) 9) 10)) identity)
üëâ 3628800
(producte-seq-cps '() identity)   üëâ 1
(producte-seq-cps  3  identity)   üëâ 3
(producte-seq-cps '(10) identity) üëâ 10
```
Aquesta funci√≥ no fa el que volem, no interromp el c√†lcul si troba un `0`. En canvi, en veiem
la possibilitat, ja que podem invocar la continuaci√≥ `cont` original, la que
es passa en la crida a `producte-seq-cps`, en trobar un `0`, o continuar
l'execuci√≥ en altre cas. En aquest cas no fem distinci√≥...

---

## _Continuation-Passing Style_: Exemples

Haur√≠em de diferenciar aquestes continuacions, l'original i les que continuen el c√†lcul.
Per a aix√≤, fem una funci√≥ auxiliar `go`:

```Clojure
(defn producte-seq-cps
  "s √©s una seq√º√®ncia multi-nivell de nombres, o un nombre"
  [s cont]
  (letfn [(go [s cont']
             (cond
               (number? s) (if (zero? s)
                             (cont 0)    ;; <-- Atenci√≥
                             (cont' s))  ;; <-- Atenci√≥
               :else (if (empty? s) (cont' 1)
                         (let [[cap & cua] s]
                           (recur cap (fn [v]
                                        (go cua (fn [w]
                                                  (cont' (* v w))))))))))]
    (go s cont)))
    
(producte-seq-cps '(((1)) 2 ((3 4) (5 6) ((((7)))) (((8)) 9) 10)) identity)
üëâ 3628800
```

Quan trobem un nombre, si √©s `0` invoquem la continuaci√≥ original, la que fem
servir a la crida original a la funci√≥ `producte-seq-cps`, en altre cas invoquem
la continuaci√≥ de la crida a `go`. Quan fem servir la continuaci√≥ original, tot all√≤
que est√† pendent de resoldre deixa d'estar-ho, senzillament no es fa.

---

## _Continuation-Passing Style_: Exemples

Ara modifiquem les funcions per afegir un `println` cada cop que trobem un nombre:

.cols5050[
.col1[
```Clojure
(producte-seq 
   '(((1)) 2 ((3 4) (5 6) (((7))))))
üëÅÔ∏è 1
üëÅÔ∏è 2
üëÅÔ∏è 3
üëÅÔ∏è 4
üëÅÔ∏è 5
üëÅÔ∏è 6
üëÅÔ∏è 7
üëâ 5040

(producte-seq 
   '(((1)) 2 ((3 0) (5 6) (((7))))))
üëÅÔ∏è 1
üëÅÔ∏è 2
üëÅÔ∏è 3
üëÅÔ∏è 0
üëÅÔ∏è 5
üëÅÔ∏è 6
üëÅÔ∏è 7
üëâ 0
```
]
.col2[
```Clojure
(producte-seq-cps 
   '(((1)) 2 ((3 4) (5 6) (((7)))))
    identity)
üëÅÔ∏è 1
üëÅÔ∏è 2
üëÅÔ∏è 3
üëÅÔ∏è 4
üëÅÔ∏è 5
üëÅÔ∏è 6
üëÅÔ∏è 7
üëâ 5040

(producte-seq-cps 
   '(((1)) 2 ((3 0) (5 6) (((7)))))
    identity)
üëÅÔ∏è 1
üëÅÔ∏è 2
üëÅÔ∏è 3
üëâ 0
```
]]

---

## _Continuation-Passing Style_: Exemples

.cols5050[
.col1[
```Clojure
(defn fibonacci [n]
  (if (< n 2) n
    (+ (fibonacci (dec n)) 
       (fibonacci (- n 2)))))

(fibonacci 10) üëâ 55
(fibonacci 40) üëâ 102334155
```
]
.col2[
```Clojure
(defn fibonacci-cps [n cont]
  (if (< n 2) (cont n)
    (recur (dec n) 
       (fn [fn1] 
         (fibonacci-cps (- n 2) 
            (fn [fn2] 
              (cont (+ fn1 fn2))))))))
               
(fibonacci-cps 10 identity) üëâ 55
(fibonacci-cps 40 identity)
üëâ Execution error (StackOverflowError)
```
]]

Aix√≠ doncs, despr√©s de veure aquests exemples la idea hauria d'estar clara: Fer
expl√≠cit el "_lloc_" on aniran a parar els resultats de tot all√≤ que
es calcula, √©s a dir, en realitat **_fem expl√≠cit el flux de
control del programa_**. All√≤ que anomenem **_continuaci√≥_** no √©s m√©s
que la funci√≥ que rebr√† el resultat del que sigui que calculi la
funci√≥ en q√ºesti√≥, aquella a la que se li ha passat la continuaci√≥ com
a argument.

Cal fixar-se en el detall que **_en CPS totes les invocacions de la continuaci√≥ sempre es
fan en tail position_**. Aix√≤ √©s precisament perqu√® la continuaci√≥ rep el valor final
d'all√≤ que es vol calcular, no cal fer cap proc√©s posterior.

---

## _Continuation-Passing Style_: _Tail Call Optimization_

Aquest estil de programar √©s poc llegible, farrag√≥s i per tant els
programadors _humans_ no l'acostumen a fer servir. El quid de la
q√ºesti√≥, per√≤, √©s que la conversi√≥ de codi _normal_ a codi en CPS es
pot programar, √©s a dir, automatitzar. Aix√≤ fa que el CPS s'hagi fet servir
sobre tot com a representaci√≥ interm√®dia per a eines que han de
manipular codi de manera autom√†tica, p.ex. compiladors.

Malgrat tot, en Clojure tenim un problema... la relaci√≥ entre CPS & TCO.red[*]:

_Every call in CPS is a tail call, and the continuation is explicitly
passed. **Using CPS without tail call optimization (TCO) will cause not
only the constructed continuation to potentially grow during
recursion, but also the call stack. This is usually undesirable**, but
has been used in interesting ways -see the Chicken Scheme compiler. As
CPS and TCO eliminate the concept of an implicit function return,
their combined use can eliminate the need for a run-time stack._

A Clojure podem resoldre-ho quan la crida √©s recursiva final, aleshores podem fer
servir `recur`. Tot i aix√≤, no hi ha manera d'aplicar TCO a la crida a la continuaci√≥,
i ens podem trobar amb un `StackOverflowError`.

.footnote[.red[*] [Font](https://en.wikipedia.org/wiki/Continuation-passing_style)]

---

## _Continuation-Passing Style_: Exercicis Recapitulatoris

* Feu versions CPS de `my-map`, `my-filter`, i `my-reduce`:

```Clojure
(defn my-map [f s]
  (if (empty? s) '()
      (let [[cap & cua] s]
        (cons (f cap) (my-map f cua)))))

(defn my-filter [p s]
  (if (empty? s) '()
      (let [[cap & cua] s]
        (if (p cap)
          (cons cap (my-filter p cua))
          (my-filter p cua)))))

(defn my-reduce [f x0 s]
  (if (empty? s) x0
      (let [[cap & cua] s]
        (recur f (f x0 cap) cua))))
```

fixem-nos, per√≤, que:

```Clojure
(my-reduce     +' 0 (range 1 100000)) üëâ 4999950000
(my-reduce-cps +' 0 (range 1 100000) identity) 
üëâ Execution error (StackOverflowError)
```

---

## _Continuation-Passing Style_

Alguns enlla√ßos interessants:

* [pulley.cps](https://github.com/positronic-solutions/pulley.cps)
  _is part of the pulley collection of libraries. It
  provides a source-to-source compiler for **transforming normal Clojure
  code to Clojure code in Continuation Passing Style (CPS)**, as well as
  runtime support for executing the transformed code._
  
* [Cascade](https://github.com/lilactown/cascade) _is a library of
  continuation-passing, thunk producing versions of many Clojure core
  functions. The goal is to allow essentially unbounded recursion and
  mutual recursion of seq operations. This means that the seq
  operations in this library must not use the call stack. Instead,
  **they use a combination of continuation-passing to ensure that
  operations can always be in the tail position** and trampolining to
  ensure that operations do not use the call stack._
  
* [Rama on Clojure‚Äôs terms, and the magic of continuation-passing style](https://blog.redplanetlabs.com/2024/10/10/rama-on-clojures-terms-and-the-magic-of-continuation-passing-style/)
  (10 d'octubre de 2024) Nathan Marz, Blog Red Planet Labs.

---

## _Continuation-Passing Style_

Si voleu llegir m√©s sobre el _Continuation-Passing Style_ (per√≤ no en Clojure):

.cols5050[
.col1[
![:scale 70%](figures/compiling.jpg)

.small[[Compiling with Continuations](https://doi.org/10.1017/CBO9780511609619)]
]
.col2[
![:scale 85%](figures/essentials.jpg)

.small[[Essentials of Programming Languages 3rd.ed.](https://eopl3.com/) Una de les principals
difer√®ncies entre les tres edicions √©s precisament l'algorisme per transformar codi a CPS]
]
]

---

## Trampolins

Si alguna cosa est√† clara a aquestes al√ßades de curs √©s com d'important es la recursivitat en
la programaci√≥ funcional, i per tant com d'important √©s fer alguna cosa respecte al problema
de l'`StackOverflowError`.

Si tingu√©ssim TCO general, el problema est√† resolt. Fent servir CPS podem passar qualsevol
funci√≥ a una funci√≥ on totes les invocacions a funci√≥ estan en _tail position_. Per√≤ a Clojure no tenim
TCO general.

La possibilitat de fer servir funcions d'ordre superior ens permet afrontar aquest problema
des d'un altre punt de vista: El **_trampolining_**.red[*].

A grans trets, la idea √©s que les funcions no retornin valors, sin√≤ _funcions que retornen valors_.
Aix√≠ no acumulem informaci√≥ a la pila, a canvi de requerir, _grosso modo_, dues crides a funci√≥
per cada crida a funci√≥ que cal fer.

Clojure t√© `trampoline`.red[*] ([clojuredocs.org/clojure.core/trampoline](https://clojuredocs.org/clojure.core/trampoline))

A la seg√ºent transpar√®ncia veurem el codi original implementat a Clojure.

.footnote[.red[*Font]: _The Joy of Clojure_, secci√≥ 7.3.3, p. 161] 

---

## Trampolins

El codi font.red[*] de la funci√≥ Clojure (sense comentari ni metadades) √©s:

```Clojure
(defn trampoline
  ([f] (let [ret (f)]
         (if (fn? ret)
           (recur ret)
           ret)))
  ([f & args] (trampoline #(apply f args))))
```

Essencialment, si invoquem `trampoline` amb una funci√≥ `f` amb arguments `args` ho convertim en un _thunk_
(funci√≥ sense par√†metres) `#(apply f args)` i invoquem `trampoline` amb un sol argument, una 
funci√≥ que suposem no t√© par√†metres.

Si invoquem `trampoline` amb una funci√≥ `f` sense par√†metres el primer que es fa √©s cridar-la `(f)` i
lligar el resultat al nom `ret`. Estem suposant en aquest punt que `ret` pot ser, o b√© un _thunk_, o b√©
quelcom que **no** √©s una funci√≥. En el primer cas tornem a invocar `trampoline`, en el segon cas retornem 
el valor.

Aix√≠ doncs, no podem invocar `trampoline` amb qualsevol mena de codi, cal adaptar les funcions que
_trampolinitzem_ als requeriments de la funci√≥ `trampoline`.

.footnote[.red[*] [L√≠nia 6370 del codi font de clojure.core](https://github.com/clojure/clojure/blob/master/src/clj/clojure/core.clj)]

---

## Trampolins

El comentari que apareix al codi font de `trampoline` √©s:

  "_trampoline can be used to convert algorithms requiring mutual
  recursion without stack consumption. Calls f with supplied args, if
  any. If f returns a fn, calls that fn with no arguments, and
  continues to repeat, until the return value is not a fn, then
  returns that non-fn value. Note that if you want to return a fn as a
  final value, you must wrap it in some data structure and unpack it
  after trampoline returns._"
  
Malgrat l'√®mfasi que posa el comentari en la recursivitat m√∫tua, `trampoline` pot
fer-se servir en situacions que no responen a aquesta casu√≠stica. Per exemple:

.cols5050[
.col1[
```Clojure
(defn my-reduce-cps [f x0 s cont]
  (if (empty? s) (cont x0)
      (let [[cap & cua] s]
        (recur f (f x0 cap) cua
               (fn [v]
                 (cont v))))))
(my-reduce-cps +' 0 
               (range 1 100000) 
               identity)
üëâ Execution error (StackOverflowError)
```
]
.col2[
```Clojure
(defn my-reduce-cps-t [f x0 s cont]
  (if (empty? s) #(cont x0)
      (let [[cap & cua] s]
        #(my-reduce-cps-t f (f x0 cap) 
                 cua
                 (fn [v]
                   (fn [] (cont v)))))))
(trampoline my-reduce-cps-t 
            +' 0 (range 1 100000) 
            identity)
üëâ 4999950000
```
]]

---

## Trampolins

Veiem un altre exemple. Recordeu `foldr`?

```Clojure
(defn foldr [f val coll]
  (if (empty? coll) val
      (f (first coll) (foldr f val (rest coll)))))

(foldr +' 0 (range 1 100000))üëâ Execution error (StackOverflowError)
```
No √©s recursiva final, per√≤ la podem convertir a recursiva final aplicant CPS:

```Clojure
(defn foldr-cps [f val coll cont]
  (if (empty? coll) (cont val)
      (recur f val (rest coll) (fn [v]
                                 (cont (f (first coll) v))))))

(foldr-cps +' 0 (range 1 100000) identity)üëâ Execution error (StackOverflowError)
```

Ja vam comentar que aquest `recur` de la versi√≥ CPS no ens evita els
`StackOverflowError` ja que les crides a `cont`, malgrat estar en
_tail position_, no s√≥n optimitzades.

---

## Trampolins

Amb `trampoline` ho podem optimitzar, a canvi d'escriure una funci√≥ lleugerament diferent:

```Clojure
(defn foldr-cps-t
  [f val coll cont]
  (if (empty? coll) #(cont val)         ;; <-- Aqu√≠ cal retornar un thunk
      #(foldr-cps-t f val (rest coll)   ;; <-- Aqu√≠ tamb√©
                    (fn [v]
                      (fn [] (cont (f (first coll) v)))))))  ;; <-- Aqu√≠ tamb√©

(trampoline foldr-cps-t +' 0 (range 1 100000) identity) üëâ 4999950000
```

Aix√≠, podr√≠em redefinir `foldr`:

```Clojure
(defn foldr [f val coll]
  (letfn [(foldr-cps-t [f val coll cont]
            (if (empty? coll) #(cont val)
                #(foldr-cps-t f val (rest coll)
                              (fn [v]
                                (fn [] (cont (f (first coll) v)))))))]
    (trampoline foldr-cps-t f val coll identity)))

(foldr +' 0 (range 1 100000)) üëâ 4999950000
```
---

## Trampolins

El _trampolining_, per√≤, no √©s de franc. T√© un cost que dep√©n, √©s clar, del nombre de crides
a funci√≥ que la funci√≥ realitza. Veiem un exemple _extrem_. Recordeu la versi√≥ CPS de `fibonacci`?

```Clojure
(defn fibonacci-cps [n cont]
  (if (< n 2) (cont n)
    (recur (dec n) (fn [fn1] 
                     (fibonacci-cps (- n 2) (fn [fn2] 
                                              (cont (+ fn1 fn2))))))))
;; per√≤...
(fibonacci-cps 40 identity) üëâ Execution error (StackOverflowError)
```
Podem redefinir la funci√≥ per fer-la adient a `trampoline`:

```Clojure
(defn fibonacci-cps-t [n cont]
  (if (< n 2) #(cont n)
      #(fibonacci-cps-t (dec n) 
         (fn [fn1] 
           (fn [] (fibonacci-cps-t (- n 2) 
                    (fn [fn2] 
                      (fn [] (cont (+ fn1 fn2))))))))))
                      
(trampoline fibonacci-cps-t 40 identity) üëâ 102334155
;; per√≤...
(time (trampoline fibonacci-cps-t 40 identity)) üëâ 102334155
üëÅÔ∏è "Elapsed time: 44639.411517 msecs" ;; m√©s de 44 segons !!!!
```
---

## Trampolins

El cas d'√∫s que s'explica a la documentaci√≥ i als llibres de Clojure √©s quasi
sempre el de funcions m√∫tuament recursives. Si s'escriuen de manera adequada per a `trampoline`,
com diu el comentari al codi font, no consumeixen pila.

L'exemple cl√†ssic:

.cols5050[
.col1[
```Clojure
(defn even-or-odd? [n]
  (letfn [(my-even? [n]
            (if (zero? n) "even"
                (my-odd? (dec n))))
          (my-odd? [n]
            (if (zero? n) "odd"
                (my-even? (dec n))))]
    (my-even? n)))

(even-or-odd? 1234) üëâ "even"
(even-or-odd? 1235) üëâ "odd"
(even-or-odd? 1)    üëâ "odd"
;; per√≤...
(even-or-odd? 1000001) 
üëâ Execution error (StackOverflowError)
```
]
.col2[
```Clojure
(defn even-or-odd?-t [n]
  (letfn [(my-even? [n]
            (if (zero? n) "even"
                #(my-odd? (dec n))))
          (my-odd? [n]
            (if (zero? n) "odd"
                #(my-even? (dec n))))]
    (trampoline my-even? n)))

(even-or-odd?-t 1234) üëâ "even"
(even-or-odd?-t 1235) üëâ "odd"
(even-or-odd?-t 1)    üëâ "odd"
;; per√≤...
(even-or-odd?-t 1000001) üëâ "odd"

```
]]

---

## Trampolins: Exercicis Recapitulatoris

* Donada la funci√≥ `(member? x s)`, que retorna un boole√† indicant si
`x` (que no √©s una seq√º√®ncia) est√† en la seq√º√®ncia `s` (que pot tenir
subseq√º√®ncies), feu una funci√≥ `member?-t` equivalent que no pateixi de
l'`StackOverflowError`.

```Clojure
(defn member?
  "x no √©s una seq√º√®ncia, s √©s una seq√º√®ncia, i pot tenir subseq√º√®ncies"
  [x s]
  (if (empty? s) false
      (let [[cap & cua] s
            membre-cap  (if (seq? cap) (member? x cap) (= x cap))]
        (or membre-cap (member? x cua)))))
        
(member? 10 '(((1)) 2 ((3 0) (5 6) ((((7)))) (((8)) 9) 10))) üëâ true
(member? 4 (range 5 50000)) üëâ Execution error (StackOverflowError)

;; en canvi

(member?-t 10 '(((1)) 2 ((3 0) (5 6) ((((7)))) (((8)) 9) 10))) üëâ true
(member?-t 4 (range 5 50000)) üëâ false
(member?-t 4564328 (range 5 5000000)) üëâ true
```
---

## Trampolins: Exercicis Recapitulatoris

* Donada la funci√≥ `(my-take-while pred s)` que retorna el prefix d'`s` m√©s gran tal
que tots els seus elements satisfan el predicat `pred`, feu una funci√≥ `(my-take-while-t pred s)`
equivalent que no tingui problemes amb la pila (no generi errors `StackOverflowError`).

```Clojure
(defn my-take-while [f s]
  (if (empty? s) '()
      (let [[cap & cua] s]
	    (if (f cap)
	      (cons cap (my-take-while f cua))
	      '()))))

(my-take-while even? [2 4 6 8 9 7 8 4]) üëâ (2 4 6 8)
(my-take-while even? [])  üëâ ()
(my-take-while even? [1]) üëâ ()
(my-take-while even? [2]) üëâ (2)
(my-take-while even? [2 4 6 8 10]) üëâ (2 4 6 8 10)

;; per√≤...

(count (my-take-while #(< % 100000) (range 1 500000))) 
üëâ Execution error (StackOverflowError)

;; en canvi...

(my-take-while-t even? [2 4 6 8 9 7 8 4]) üëâ (2 4 6 8)
(count (my-take-while-t #(< % 100000) (range 1 500000))) üëâ 99999

```
---

## Trampolins: Exercicis Recapitulatoris

* Volem resoldre aquest problema: _Escriure una funci√≥_ `maxkd(m,k)` _que
retorni el nombre m√©s gran, de mida com a molt_ `k`, _format pels digits
d' _`m` _(en el mateix ordre en que apareixen a_ `m`_)_ i tenim una soluci√≥ que
funciona b√©:

```Clojure
(defn maxkd [m k]
  (if (or (zero? m) (zero? k)) 0
      (let [a  (+ (mod m 10) (* 10 (maxkd (quot m 10) (dec k))))
            b  (maxkd (quot m 10) k)]
        (max a b))))
```
per√≤ volem passar-la a CPS per tenir una versi√≥ recursiva final:

```Clojure
(defn maxkd-cps [m k cont]
  (if (or (zero? m) (zero? k)) (cont 0)
      (recur (quot m 10) k
             (fn [v]
               (maxkd-cps (quot m 10) (dec k)
                          (fn [w]
                            (cont (max v (+ (mod m 10) (* 10 w))))))))))
```
.right[(continua ‚û°Ô∏è)]

---

## Trampolins: Exercicis Recapitulatoris

El problema √©s el seg√ºent:

```Clojure
(maxkd 1917 2) üëâ 97
(maxkd 5674329856473 10) üëâ 7439856473

;; per√≤...

(maxkd-cps 1917 2 identity) üëâ 97
(maxkd-cps 5674329856473 10 identity) üëâ Execution error (StackOverflowError)
```

.small[**Exercici:** Per quina ra√≥ penseu que la versi√≥ "normal" funciona b√© i en canvi
la versi√≥ CPS de seguida arriba a l'`StackOverflowError`?]

Com podr√≠em aprofitar `maxkd-cps` per fer una versi√≥ `(maxkd-t m k)` que no provoqui
errors `StackOverflowError`?

```Clojure
(maxkd 567432985647394320009214894231 25) üëâ 7985647394320009214894231N

(maxkd-cps 567432985647394320009214894231 25 identity)
üëâ Execution error (StackOverflowError)

(maxkd-t 567432985647394320009214894231 25) üëâ 7985647394320009214894231N
```

---

## _Transducers_. . .
